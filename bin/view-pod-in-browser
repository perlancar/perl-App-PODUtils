#!perl

# DATE
# VERSION

use 5.010001;
use strict;
use warnings;

use App::PODUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{view_pod_in_browser} = {
    v => 1.1,
    summary => 'Render a POD file into HTML and view it in browser',
    args => {
        pod_file => {
            schema => 'str*',
            req => 1,
            pos => 0,
        },
    },
    deps => {
        prog => 'pod2html',
    },
};
sub view_pod_in_browser {
    require Browser::Open;
    #require File::Slurper;
    require File::Temp;
    require File::Util::Tempdir;

    my %args = @_;
    my $pod_file = $args{pod_file};

    return [404, "No such file"] unless -f $pod_file;


    my $tmp_dir = File::Util::Tempdir::get_tempdir();
    my $cache_dir = File::Temp::tempdir(CLEANUP => 1);
    my ($html_fh, $html_file) = File::Temp::tempfile(
        DIR=>$tmp_dir, SUFFIX=>".html");
    system(
        "pod2html",
        "--infile", $pod_file,
        "--outfile", $html_file,
        "--cachedir", $cache_dir,
    );
    return [500, "Can't pod2html: $!"] if $?;
    my $err = Browser::Open::open_browser("file:$html_file");
    return [500, "Can't open browser"] if $err;
    [200];
}

Perinci::CmdLine::Any->new(
    url => '/main/view_pod_in_browser',
)->run;

# ABSTRACT:
# PODNAME:

=head1 DESCRIPTION
